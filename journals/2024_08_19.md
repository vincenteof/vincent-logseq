- [[merkle tree]] 原理 #[[Crypto Roadmap]]
	- 他的主要作用是快速检查一些元素是否被包含，但是和类似于 hash set 这种数据结构不同的是，用户可以通过给出 merkle proof 向其他人证明某些元素确实存在而无需验证者得到整个数据集。因此他十分适用于类似分布式系统这种领域，节点可能是不能互相信任的。
	- 实现基于 merkle tree 的 verify 通常有两种方式，常用的方式在计算 hash 时会保留 node 间的顺序，通过 index 来分析出当前结点是左结点还是右结点，每次计算完通过 index 除以 2 达到上一层
		- ```solidity
		      function verifiy(
		          bytes32[] memory proof,
		          bytes32 root,
		          bytes32 leaf,
		          uint256 index
		      ) internal pure returns (bool) {
		          bytes32 hash = leaf;
		          for (uint256 i = 0; i < proof.length; i++) {
		              bytes32 proofElement = proof[i];
		              hash = index % 2 == 0
		                  ? keccak256(abi.encodePacked(hash, proofElement))
		                  : keccak256(abi.encodePacked(proofElement, hash));
		              index /= 2;
		          }
		          return hash == root;
		      }
		  ```
	- 第二种方式假设在构建 merkle tree 的时候，计算 hash 时会根据两个结点的 hash 大小来连接，这样可以避免计算 index
		- ```solidity
		      function verifyBySorting(
		          bytes32[] memory proof,
		          bytes32 root,
		          bytes32 leaf
		      ) internal pure returns (bool) {
		          bytes32 hash = leaf;
		          for (uint256 i = 0; i < proof.length; i++) {
		              bytes32 proofElement = proof[i];
		              hash = hash < proofElement
		                  ? keccak256(abi.encodePacked(hash, proofElement))
		                  : keccak256(abi.encodePacked(proofElement, hash));
		          }
		          return hash == root;
		      }
		  ```